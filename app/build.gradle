apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'

def signingProperties = getSigningProperties()

android {
	compileSdkVersion 25
	buildToolsVersion "25.0.3"

	defaultConfig {
		applicationId "com.voonapp.boilerplate"
		minSdkVersion 15
		targetSdkVersion 25
		versionCode getVersion('code')
		versionName getVersion('name')

		vectorDrawables.useSupportLibrary = true

		jackOptions {
			enabled true
		}

		applicationVariants.all { variant ->
			variant.outputs.each { output ->
				def apkName = "${appName}_v${versionName}_c${versionCode}.apk"
				output.outputFile = new File(output.outputFile.parentFile, apkName)
			}
		}
	}

	signingConfigs {
		release {
			keyAlias signingProperties['KEY_ALIAS']
			keyPassword signingProperties['KEY_PASSWORD']
			storeFile file(signingProperties['STORE_FILE'])
			storePassword signingProperties['STORE_PASSWORD']
			v1SigningEnabled true
			v2SigningEnabled true
		}
	}

	buildTypes {
		debug {
			applicationIdSuffix '.debug'
			versionNameSuffix '-DEBUG'
		}
		release {
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
			signingConfig signingConfigs.release
		}
	}

	productFlavors {
		paid {
			versionNameSuffix '-PAID'
		}
		free {
			versionNameSuffix '-FREE'
		}
	}

	aaptOptions {
		ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:.*:<dir>_*:!CVS:!thumbs.db:!picasa.ini:!*~'
	}

	compileOptions {
		encoding "UTF-8"
		incremental true
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	dexOptions {
		dexInProcess true
	}

	lintOptions {
		abortOnError false
	}
}

greendao {
	schemaVersion getVersion('database')
}

dependencies {
	compile "com.android.support:appcompat-v7:${androidVersion}"

	// Networking, caching, and images
	compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
	compile "com.squareup.retrofit2:converter-moshi:${retrofitVersion}"
	compile "com.squareup.okhttp3:okhttp:${okhttpVersion}"
	compile "com.squareup.okhttp3:logging-interceptor:${okhttpVersion}"
	compile 'com.squareup.picasso:picasso:2.5.2'

	// SQLite Database
	compile 'org.greenrobot:greendao:3.2.2'

	// LeakCanary
	debugCompile "com.squareup.leakcanary:leakcanary-android:${leakcanaryVersion}"
	releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:${leakcanaryVersion}"
	testCompile "com.squareup.leakcanary:leakcanary-android-no-op:${leakcanaryVersion}"
}

static def getVersion(String type) {
	def major = 0
	def minor = 0
	def patch = 0

	switch (type) {
		case 'code':
		case 'database':
			return major * 10000 + minor * 100 + patch
		case 'name':
			return "$major.$minor.$patch"
	}
}

def getSigningProperties() {
	def Properties properties = new Properties();
	def propertiesFile = file('../signing.properties')

	if (propertiesFile.canRead()) {
		properties.load(new FileInputStream(propertiesFile))

		if (properties != null && properties.containsKey('STORE_FILE') && properties.containsKey('STORE_PASSWORD') && properties.containsKey('KEY_ALIAS') && properties.containsKey('KEY_PASSWORD')) {
			return properties
		} else {
			throw new Exception("Some entries are missing ! Entry STORE_FILE, STORE_PASSWORD, KEY_ALIAS and KEY_PASSWORD must be present in the signing.properties file.")
		}
	} else {
		throw new FileNotFoundException("File not found ! Please create a signing.properties file under root project.")
	}
}
