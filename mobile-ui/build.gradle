/*
 * Copyright (c) 2017 Voonder
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
  compileSdkVersion 26
  buildToolsVersion "26.0.1"

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion 26

//    multiDexEnabled = true

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  signingConfigs {
    debug {
      keyAlias 'OAB JN'
      keyPassword 'OAB_android'
      storeFile rootProject.file("buildsystem/keystore/debug.jks")
      storePassword 'OAB_android'
    }
    release {
      keyAlias 'OAB JN'
      keyPassword 'OAB_android'
      storeFile rootProject.file("buildsystem/keystore/release.jks")
      storePassword 'OAB_android'
    }
  }

  buildTypes {
    debug {
      versionNameSuffix '-DEBUG'
      proguardFiles rootProject.file("buildsystem/proguard/proguard-rules-debug.pro")
      signingConfig signingConfigs.debug
      testCoverageEnabled true
    }
    release {
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), rootProject.file("buildsystem/proguard/proguard-rules.pro")
      signingConfig signingConfigs.release
    }
  }

  sourceSets {
    def pathApt = "$buildDir/generated/source/kapt"

    main.java.srcDirs += 'src/main/kotlin'
    main.java.srcDirs += [file("$pathApt/main")]

    debug.java.srcDirs += [file("$pathApt/debug")]
    release.java.srcDirs += [file("$pathApt/release")]

    test.java.srcDirs += 'src/test/kotlin'

    androidTest.java.srcDirs += 'src/androidTest/kotlin'
  }

  aaptOptions {
    ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:.*:<dir>_*:!CVS:!thumbs.db:!picasa.ini:!*~'
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

//  dexOptions {
//    preDexLibraries = false
//    dexInProcess = false
//    javaMaxHeapSize "4g"
//  }

  lintOptions {
    quiet true
    abortOnError false
    ignoreWarnings true
    disable 'InvalidPackage' // Some libraries have issues with this.
    disable 'OldTargetApi' // Lint gives this warning but SDK 20 would be Android L Beta.
    disable 'IconDensities' // For testing purpose.
    disable 'IconMissingDensityFolder' // For testing purpose.
  }

  packagingOptions {
    exclude 'LICENSE.txt'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/ASL2.0'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/LICENSE'
  }
}

kapt {
  correctErrorTypes = true
  generateStubs = true
}

dependencies {
  def mobileUiDependencies = rootProject.ext.mobileUiDependencies
  def mobileUiTestDependencies = rootProject.ext.mobileUiTestDependencies

  implementation project(':cache')
  implementation project(':data')
  implementation project(':domain')
  implementation project(':presentation')
  implementation project(':remote')

  implementation mobileUiDependencies.appCompatV7
  implementation mobileUiDependencies.dagger
  implementation mobileUiDependencies.daggerSupport
  implementation mobileUiDependencies.glide
  implementation mobileUiDependencies.javaxInject
  implementation mobileUiDependencies.kotlin
  implementation mobileUiDependencies.rxAndroid
  implementation mobileUiDependencies.rxKotlin

  debugImplementation mobileUiDependencies.leakCanary

  releaseImplementation mobileUiDependencies.leakCanaryNoOp

  androidTestImplementation mobileUiTestDependencies.junit
  androidTestImplementation mobileUiTestDependencies.mockito
  androidTestImplementation mobileUiTestDependencies.mockitoAndroid
  androidTestImplementation(mobileUiTestDependencies.espressoContrib) {
    exclude module: 'appcompat'
    exclude module: 'appcompat-v7'
    exclude module: 'support-v4'
    exclude module: 'support-v13'
    exclude module: 'support-annotations'
    exclude module: 'recyclerview-v7'
    exclude module: 'design'
  }
  androidTestImplementation(mobileUiTestDependencies.espressoCore) {
    exclude group: 'com.android.support', module: 'support-annotations'
  }
  androidTestImplementation(mobileUiTestDependencies.espressoIntents) {
    exclude group: 'com.android.support', module: 'support-annotations'
  }
  androidTestImplementation(mobileUiTestDependencies.androidRunner) {
    exclude group: 'com.android.support', module: 'support-annotations'
  }
  androidTestImplementation(mobileUiTestDependencies.androidRules) {
    exclude group: 'com.android.support', module: 'support-annotations'
  }

  kapt mobileUiDependencies.daggerCompiler

  kaptTest mobileUiDependencies.daggerCompiler

  kaptAndroidTest mobileUiDependencies.daggerCompiler
}
